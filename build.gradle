plugins {
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def isShellApp = project.getName() == rootProject.shellApp
def isLaunchApp = project.getName() == rootProject.launchApp

if (isShellApp) {
    println "isShellApp: ${isShellApp}"
}

if (isLaunchApp) {
    println "isLaunchApp: ${isLaunchApp}"
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


apply from: "${rootDir}/config/buildConfigs.gradle"
apply from: "${rootDir}/config/optionConfigs.gradle"
apply from: "${rootDir}/config/featureExt.gradle"
apply from: "${rootDir}/config/versionExt.gradle"
apply from: "${rootDir}/config/signingConfigs.gradle"
apply from: "${rootDir}/config/dependencyExt.gradle"

android {
    resourcePrefix 'sampkt_'

    defaultConfig {
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            if (isLaunchApp) {
                shrinkResources feature.isMinifyEnabled
            }
        }

        release {
            if (isLaunchApp) {
                shrinkResources true
            }
        }
    }

    flavorDimensions 'library'
    productFlavors {
        if (isLaunchApp) {
            app {
                dimension 'library'
                applicationId 'com.githubyss.sample_kotlin'
                versionCode 1
                versionName "1.0"
                manifestPlaceholders = [APP_NAME: "Kotlin 模板"]
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
                srcDirs 'src/main/kotlin'
            }
        }

        if (isLaunchApp) {
            app {
                java {
                    srcDirs 'src/app/java'
                    srcDirs 'src/app/kotlin'
                }
            }
        }

        test {
            java {
                srcDirs 'src/test/java'
                srcDirs 'src/test/kotlin'
            }
        }

        all {
            // set -> println "集合 ${set.name} 的文件是 ${set.java.srcDirs}"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':CommonKitSdk_Android')

    implementation dependenciesLib.kotlinx_coroutines_core
    implementation dependenciesLib.kotlinx_coroutines_android
    implementation dependenciesLib.kotlinx_coroutines_test
}